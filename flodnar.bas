1 REM THE CRYPT OF FLODNAR/YOU MUST FIRST RUN THE PROGRAM 'KRAGNOR' BEFORE ATTEMPTING TO RUN THE 'FLODNAR'
2 REM BEFORE RUNNING 'FLODNAR' <POKE25,6:POKE26,1:NEW>
3 REM 'FLODNAR' HAS A MACHINE LANGUAGE PROGRAM WITH A STARTING ADDRESS OF DECIMAL 32712
4 CLEAR200,32711
5 DIM W(100),UD(49),D(50),O$(15),L$(49),L2(49),A$(37),B$(37),C$(50),D$(50),B1(37),B(50),C(50):FORX=1TO37:READA$(X):NEXT
6 DATA T7O3L4G,O4L2C,P20,L4C,L2D,P20,L4D,E,G,E,L2C,O3L4G,O4L2C,P220,L4C,L2D,P20,L4D,L2.E,L2C,O3L4G,O4L2C,P20,L4C,L2D,P20,L4D,E,G,E,L2.C,L4A,P2,L2D,L4F,L2.E,L2C
7 REM GOTO130
8 CLS0:PRINT@230,"the crypt of flodnar";:POKE1257,128:POKE1263,128:POKE1266,128:PRINT@401,"by";:PRINT@434,"greg";:PRINT@469,"clark";:FORX=1TO4000:NEXT
9 CLSY:FORX=70TO420STEP64:PRINT@X,"THE crypt OF flodnar";:W=W+1:PLAYA$(W):NEXT:Y=Y+1:IFY>8THENY=0
10 FORX=422TO80STEP-64:PRINT@X,"the CRYPT of FLODNAR";:W=W+1:PLAYA$(W):NEXT:Y=Y+1:IFY>8THENY=0
11 Z=Z+1:IFW=36THENPLAYA$(37):W=0
12 IFZ<9THEN9
13 FORX=1TO400:NEXT
14 CLS:DEFUSR0=32712:GOTO395
15 CLS7:FOR X=1TO55:READCS:POKEX+32711,CS:NEXT
16 FORX=1TO37:READB$(X):NEXT
17 FORX=3TO50:READC$(X) :NEXT
18 FORX=1TO50:READD$(X):NEXT
19 FORX=1TO37:READB1(X):NEXT
20 FORX=0TO15:READO$(X):NEXT
21 FORX=1TO49:READL$(X):NEXT
22 IFJP=2THENFORX=1TO49:READX9:NEXTELSEFORX=1TO49:READL2(X):NEXT
23 IFJP=2THENFORX=1TO49:READX9:NEXTELSEFORX=1TO49:READUD(X):NEXT
24 IFL2(6)=1THENL$(6)="ROOM W/DAMP FLOOR"
25 IFL2(16)=10THENL$(16)="HALL"
26 IFL2(38)=5THENL$(38)="HALL"
27 IFC$(1)=""THENC$(1)="MAJOR WEAPON"
28 IFC$(2)=""THENC$(2)="MINOR WEAPON"
29 REM PERMENANT VALUES
30 IFJP<2THEND(30)=15:D(36)=19:D(35)=47:D(17)=24:D(15)=8:D(13)=36:D(14)=37:D(12)=21:D(4)=35:N1=30:D(34)=12:D(33)=20:D(29)=10:D(27)=9:D(22)=16:D(19)=25:D(10)=45:D(9)=43:D(8)=42:V=7:H=0:L=7:H1=1:UP=1:DO=0:E=1:D(6)=18:D(37)=48:D(39)=7:D(40)=6:D(48)=27
31 IFJP<>2THENS3=24:JI=36:C(33)=23:C(35)=41:C(37)=46:C(38)=10:C(39)=44:C(41)=45:C(43)=50:C(44)=8:C(45)=14:C(46)=25:C(47)=18:C(48)=37:D(50)=31:C(25)=34:C(29)=52
32 JP=2
33 CLS:PRINT@0,"";:CLS8:X=USR(0):PRINT@0,"location";:POKE1032,58:PRINT@32,"moves";:POKE1061,58:PRINT@64,"i see";:POKE1089,32:POKE1093,58:PRINT@196,"what do you wish to do";:POKE1224,32:POKE1227,32:POKE1231,32
34 PRINT@486,"the crypt of flodnar";:POKE1513,128:POKE1519,128:POKE1522,128
35 POKE1236,32:POKE1239,32:POKE1242,32:PRINT@160,"vitality";:POKE1192,58
36 S5=(SK/20)
37 PRINT@128,"i have";:POKE1153,32:POKE1158,58:POKE1216,60:POKE1217,60:POKE1218,60:POKE1219,60:POKE1243,63:POKE1244,62:POKE1245,62:POKE1246,62:POKE1247,62:GOSUB246:IFL=7THENPRINT@58,"U";
38 D(4)=35:S3=S3-S5:GOSUB246:GOSUB414:GOSUB329:GOSUB336
39 IFL=18AND MQ=0AND MR=0THENMQ=1:L$(25)="SAND FILLED ROOM":L2(25)=4:L2(32)=16:MR=1:PRINT@320,"WHAT WAS THAT!!??";
40 P$="":C=0:B=0:D=0:IFCK>8THENCK=8
41 IFC(45)=0THENC(45)=14
42 IFJE=1ANDC(3)=1ANDJF=0THENR2=RND(12)+RND(12)+RND(12):IFR2<4ORR2>33THENPRINT@416,"THE ";C$(3);" BROKE";:JF=1:FORZ=1TO50:IFC(Z)=2THENC(Z)=L+5:NEXT:S3=S3+S5:GOTO38ELSENEXT:S3=S3+S5:GOTO38
43 IFC(3)<>1THEN47ELSER2=RND(8)+RND(8)+RND(8)+RND(8)+RND(8):IFR2<7 OR R2>38THEN44ELSE48
44 FORX=50TO1STEP-1:IFC(X)=2THEN45ELSENEXT:GOTO48
45 C(X)=46:FORX=1TO24:PLAYA$(X):NEXT:PRINT@385,"A WEASEL WAS IN MY PACK-IT RAN";:PRINT@429,"AWAY!!";
46 FORX=25TO37:PLAYA$(X):NEXT
47 IFL=31THENW=1
48 PRINT@9,L$(L);
49 IFL2(L)=16THENGOSUB414:GOTO51
50 PRINT@43,O$(L2(L));
51 IFUP=1THENPRINT@58,"U";ELSEIFDO=1THENPRINT@61,"D";
52 REM A=0:B=0:C=0:D=0
53 PLAY"T2L100;O4;E"::PRINT@224,"";:LINEINPUT"";A$:A1$=A$
54 IFA$="LOOK BOOK"ANDC(15)=1THENX=USR(0):PRINT@256,"HOW TO WIN FRIENDS AND INFLUENCEDRAGONS";:GOTO38
55 IFA$="HELP"THENX=USR(0):GOTO434
56 IFLEN(A$)<1THENPLAY"L80;O2;AP10D":FORX=1248TO1279:POKEX,207:NEXT:GOTO53
57 X=USR(0)
58 IFA$="EAT"OR A$="DRINK"THEN446
59 IFL=38AND N1>0AND A$="PET DRAGON"THENPRINT@256,"I DON'T THINK IT LIKED IT";:PRINT@288,"IT FLASH FRIED MY HAND!";:S3=S3-(S3/2):GOTO38
60 IFLEN(A$)=1THEN72
61 FORB=1TO37:WW$=B$(B):WW=INSTR(1,A$,WW$):IFWW>0THENVW=LEN(B$(B)):GOTO65ELSENEXT:B=0:PRINT@257,"I DON'T UNDERSTAND WHAT IT IS";:PRINT@294,"YOU WANT ME TO DO.";:PRINT@325,"TRY A DIFFERENT VERB";
62 ER=((INT(32-LEN(A$))/2)+351):IFLEN(A$)>30THENER=352
63 IFLEN(A1$)<30THENPRINT@ER,CHR$(159);A1$;CHR$(159);ELSEA1$=LEFT$(A1$,30):PRINT@ER,CHR$(159);A1$;CHR$(159);
64 GOTO38
65 FORY=1TOVW:MID$(A$,WW,1)=" ":WW=WW+1:NEXT:B=B1(B)
66 FORC=1TO50:WW$=C$(C):WW=INSTR(1,A$,WW$):IFWW>0THENGOTO86ELSENEXT:C=0:GOTO67
67 FORD=1TO50:WW$=D$(D):WW=INSTR(1,A$,WW$):IFWW>0THEN86ELSENEXT:D=0
68 PRINT@257,"AFTER CONSIDERABLE SEARCHING,";:PRINT@291,"I CANNOT FIND THAT OBJECT";:PRINT@327,"TRY ANOTHER NOUN";
69 ER=((INT(32-LEN(A$))/2)+351):IFLEN(A$)>30THENER=352
70 IFLEN(A1$)<30THENPRINT@ER,CHR$(175);A1$;CHR$(175);ELSEA1$=LEFT$(A1$,30):PRINT@ER,CHR$(175);A1$;CHR$(175);
71 GOTO38
72 IFA$="U"ANDUP=1THENV=V-1:GOTO291
73 IFL=24ANDMK=1THENC(4)=36
74 IFA$="D"ANDDO=1THENV=V+1:GOTO291
75 IFL=2ANDC(4)=1ANDMO=1THENPRINT@256,"I CAN'T THE ROPE IS STILL TIED";:GOTO38
76 IFA$="N"AND N=1THENV=V-1:GOTO291
77 IFA$="S"ANDS=1THENV=V+1:GOTO291
78 IFA$="E"ANDE=1THENH=H+7:GOTO291
79 IFA$="W"ANDW=1THENH=H-7:GOTO291
80 IFA$="A"THENB=1ANDC=3:GOTO316
81 REM
82 IFA$="Q"ORA$="P"THEN324
83 IFA$="&"THEN S2=S2-99:GOTO33
84 IFA$="R"AND(L=7OR L=15)THEN373
85 PLAY"DAD":PRINT@256,"I CAN'T OBEY";:GOTO38
86 REM
87 IFB=3THENGOTO341
88 IFB=2THENGOTO347
89 IFB=4THENP$="":PRINT@224,"";:PLAY"DA":LINEINPUT"WHERE?";P$:IFP$="SACK"THENX=USR(0):GOTO365ELSEX=USR(0)
90 REM SELECT RESULTS BY LOCATION
91 H1=(H+7)/7
92 ON V GOTO93,128,150,183,204,216,226
93 REM V1
94 IFH1=6ANDB=21ANDMG=0ANDC=8ANDC(8)=1THEN95ELSE97
95 IFMM=1THENPRINT@256,"OK I'M DOWN";:D(8)=42:L=37:V=2:H=35:GOTO38ELSER2=RND(8):IFR2<4THENPRINT@256,"OOOMPH!! THE POLE BROKE AND I   FELL DOWN TO THE FLOOR BELOW!   KRAGNOR WILL PAY FOR THIS!";:MG=1:C$(8)="BROKEN POLE":GOTO38
96 PRINT@256,"SHAKEY,BUT I MADE IT!!";:D(8)=42:V=2:L=37:H=35:GOTO38
97 IFH1=6ANDB=1ANDD=8THENPRINT@256,"JUST AN OPEN TRAP DOOR";:GOTO38
98 IFH1=3THEN99ELSE100
99 IFB=1ANDD=33THENPRINT@256,"IT READS 'FROM HERE YOU MAY GO BACK TO    KRAGNOR'S EMPORIUM BY INPUTING <R> OR GO BACK INTO THE GAME BY INPUTING <W>'";:GOTO38
100 IFH1=7THEN101ELSE105
101 IFB=1ANDD=37THENPRINT@256,"A METAL CABINET WITH ONE DRAWER";:D(38)=48:GOTO38
102 IFB=5ANDD=38ANDJS=0THEN103ELSE104
103 JS=1:C(40)=48:R2=RND(18)+RND(18)+RND(18):IFR2>40THENPRINT@256,"A PURPLE GAS CAME OUT!!!";:FORZ=1TO2000:NEXT:PRINT@320,"I FEEL WEAKER!!";:S3=S3-RND(5)+5:GOTO38ELSEPRINT@256,"IT IS OPEN";:GOTO38
104 REM
105 IFH1=1THEN106ELSE110
106 IFB=1ANDD=40THENPRINT@256,"I SEE A HIDDEN LATCH";:D(41)=6:GOTO38
107 IFB=1ANDD=41ANDD(41)=6THENPRINT@256,"SMALL,HOOK SHAPED";:GOTO38
108 IFB=1ANDD=42ANDD(42)=6THENIFC(31)=0THENC(31)=6:PRINT@256,"WOW!!";:GOTO38ELSEPRINT@256,"NOTHING ELSE";:GOTO38
109 IFB=5ANDD=41ANDD(42)=0THENR2=RND(20)+RND(20):D(42)=6:PRINT@256,"A SMALL PANEL SLID BACK,THERE ISA CUBBY HOLE BEHIND IT";:IFR2<7THENPRINT@320,"A BROWN SPIDER WITH RED SPOTS   BIT MY HAND!!";:S3=S3-RND(8)+4:GOTO38ELSEGOTO38
110 IPH1=4THEN111ELSE126
111 IFB=1THEN112ELSE119
112 IFD(D)=27THEN113ELSE119
113 IFD=48THENPRINT@256,"GOES FROM ONE WALL TO THE OTHER,THERE IS A PADLOCK ON A HASP";:D(43)=27:D(44)=27:GOTO38
114 IFD=43THENPRINT@256,"VERY HEAVY W/KEYHOLE";:GOTO38
115 IFD=45THENIFC(28)=0THENC(28)=27:PRINT@256,"THERE IS A KEY HANGING FROM IT";:GOTO38
116 IFD=46THENPRINT@256,"THERE ARE TWELVE LETTERS ON IT- A THROUGH L";:GOTO38
117 IFD=47THEND(46)=27:PRINT@256,"THERE IS A DIAL ON IT";:GOTO38
118 IFD=49THENPRINT@256,"IT IS HAND-WRITTEN,THE LETTERS B+D+J";:GOTO38
119 REM
120 IFB=8ANDD=46ANDJU=0THENPRINT@256,"TO WHAT LETTER";:INPUTMZ$:IFMZ$="B"THENPRINT@288,"TO WHAT LETTER";:INPUTMZ$:IFMZ$="D"THENPRINT@320,"TO WHAT LETTER";:INPUTMZ$:IFMZ$="J"THENX=USR(0):PRINT@256,"'CLICK'";:GOSUB412:PRINT@256,"IT OPENED";:JU=1
121 IFB=8ANDD=46ANDJU=0THENX=USR(0):PRINT@256,"THAT IS NOT CORRECT";:GOTO38
122 IFB=8ANDD=46ANDJU=1THENC(32)=27:C(34)=27:D(46)=99:GOTO38
123 IFB=5ANDD=43ANDJT=0ANDC(28)=1THENJT=1:D(48)=99:D(43)=99:D(44)=99:D(45)=99:PRINT@256,"IT SLID BACK IN THE WALL,NOW I  SEE A BRASS DOOR INLET TO THE   FLOOR";:D(47)=27:GOTO38
124 IFB=1ANDD=5THENPRINT@256,"I SEE SOMETHING WRITTEN";:D(49)=27:GOTO38
125 IFB=1ANDD=1THENPRINT@256,"A SMALL HOOK";:D(45)=27:GOTO38
126 REM
127 GOTO235
128 REM V2
129 IFH1=6ANDB=1ANDD=8THENPRINT@256,"IT IS IN THE CEILING,THE CEILINGIS ABOUT 8 FEET HIGH";:IFMD=0THENPRINT@320,"THE TRAP DOOR IS CLOSED";:GOTO38ELSEPRINT@320,"THE TRAP DOOR IS OPEN";:GOTO38
130 IFH1=6ANDB=19ANDD=8THEN131ELSE133
131 PRINT@224,"WITH WHAT";:INPUTA$:X=USR(0):IFA$=C$(8)THEN132ELSE133
132 IFMD=1THENPRINT@256,"IT IS ALREADY OPEN";:GOTO38ELSEIFMD=0THENPRINT@256,"IT IS NOW OPEN";:MD=1:GOTO38
133 IFH1=6ANDMG=0ANDMD=1ANDC(8)=1ANDB=21ANDC=8THEN134ELSE136
134 IFMM=1THENPRINT@256,"OK,I'M UP";:D(8)=41:L=36:H=35:V=1:GOTO291:ELSER2=RND(10):IFR2<4THENPRINT@256,"OOOMPH!! THE POLE BROKE AND I  FELL BACK ON THE FLOOR! KRAGNOR WILL PAY FOR THIS!";:MG=1:C$(8)="BROKEN POLE":GOTO38
135 PRINT@256,"SHAKEY,BUT I MADE IT!!";:V=1:H=35:L=36:D(8)=41:GOTO38
136 IFH1=5THEN137ELSE143
137 IFB=5ANDD=4THENIFMB=0THENPRINT@256,"LOCKED-I CAN'T";:GOTO38ELSEIFMB=1THENPRINT@256,"IT'S OPEN ALREADY";:GOTO38
138 IF B=1ANDD=4THENPRINT@256,"THERE ARE THREE LEVERS,THEY CAN BE MOVED UP AND DOWN";:D(11)=35:GOTO38
139 IFB=1ANDD=11THENPRINT@256,"THEY EACH MAY BE PLACED IN FOUR POSITIONS-A,B,C,D. ORIGINAL POS-ITIONS ARE #1-B #2-D #3-C";:GOTO38
140 IFB=22ANDD=11ANDMB=0THEN141ELSE143
141 SOUND100,1:CO=0:PRINT@256,"PLACE#1 WHERE(A,B,C,D)";:INPUTA7$:X=USR(0):SOUND100,1:PRINT@256,"PLACE#2 WHERE(A,B,C,D)";:INPUTA8$:X=USR(0):SOUND100,1:PRINT@256,"PLACE#3 WHERE(A,B,C,D)";:INPUTA9$:GOSUB436
142 X=USR(0):IFCO=3THENPRINT@256,"'CLICK'";:L2(30)=4:PRINT@320,"THE DOOR SWUNG INWARD";:MB=1:W=1:GOTO38ELSEPRINT@256,CO;"LEVER(S) ARE CORRECTLY PLACED";:GOTO38
143 IFH1=3THEN144ELSE145
144 IFB=1ANDD=12ANDD(12)=21THENPRINT@256,"THEY COME OUT OF THE FLOOR,FROM ONE WALL TO THE OTHER,AND GO TO THE CEILING OF THE HALLWAY,TRULYA WALL OF FLAME";:GOTO38
145 IFH1=1THEN146ELSE148
146 IFMO=1ANDMN=0ANDC(4)=1ANDB=16ANDC=4THENPRINT@256,"IN THE DISTANCE A RUSH OF WATER,THEN A LOUD CRASH";:L2(3)=l6:MP=1:MN=1:L$(3)="BLOCKED BY STEEL GATE":HA=HA-1:C(4)=7:GOSUB460:IFD(44)=8THEND(44)=0:GOTO38ELSEGOTO38
147 IFB=1ANDD=39THENPRINT@256,"A RED ARROW POINTING DOWNWARD,ONIT IS WRITTEN 'AUTHORIZED BEINGSONLY'";:GOTO38
148 REM
149 GOTO235
150 REM V3
151 IFH1=6ANDB=1ANDD=9ANDME=0THENPRINT@256,"A GENTLE LOOKING BEAST,PUFFS OF LOVELY LAVENDER SMOKE COMING OUTOF IT'S NOSTRILS,DELIGHTFULLY   SHAPED CLAWS,";:FORX=1TO7500:NEXT:PRINT@384,"!!!???THE UGLY CREATURE CAN WARPYOUR THOUGHTS!!";:GOTO38
152 IFH1=6ANDB=1ANDD=9THENPRINT@256,"DEAD";:IFC(36)=0THENC(36)=43:GOTO38ELSEGOTO38
153 IFH1=5THEN154ELSE157
154 IFB=23ANDC=4THEN155ELSE157
155 PRINT@256,"TO WHAT";:INPUTA$:X=USR(0):IFA$=C$(8)ANDC(8)=1ANDC(4)=1THEN156ELSEPRINT@256,"I DON'T HAVE THEM BOTH AT HAND";:GOTO38
156 MH=1:PRINT@256,"OK,I PLACED THE POLE ACROSS THE HOLE,THE ROPE IS HANGING DOWN";:C(4)=36:C(8)=36:HA=0:GOTO38
157 IFH1=5THEN158ELSE167
158 IFB=21ANDC=4THEN159ELSE167
159 IFML=1AND MM=1THEN166ELSE160
160 IFML=0THENPRINT@256,"I CLIMBED DOWN,I WAS AT LEAST   100' FROM THE FLOOR BELOW,SO,I  CLIMBED BACK UP";:GOTO38
161 MI=0:IFML=2THEN MI=MI+3
162 IFMM=0THENMI=MI+3
163 IFMG=1THENMI=MI+4
164 N2=RND(10):IFN2<MI THENIFMM=0THENPRINT@256,"THE POLE BROKE-I FELL DOWN AND  LANDED HARD!!!";:S3=S3-9:L=32:W=1:V=4:H=28:MG=1:C$(8)="BROKEN POLE":C(8)=37:C(4)=37:MK=0:IFMQ=1THENL2(32)=0:GOTO295ELSE38
165 IFN2<MI THENPRINT@256,"THE ROPE BROKE-I FELL TO THE    FLOOR BELOW-HARD!!";:S3=S3-5:L=32:W=1:V=4:H=28:C$(4)="BROKEN ROPE":MJ=1:C(4)=37:C(8)=36:MK=0:IFMQ=1THENL2(32)=0:GOTO295ELSE38
166 L=32:H=28:V=4:W=1:GOSUB413:PRINT@256,"IT'S A LONG CLIMB,BUT I MADE IT";:MK=1:C(8)=36:C(4)=37:IFMQ=1THENL2(32)=0:GOTO295ELSE38
167 IFH1=1THEN168ELSE176
168 IFB=16ANDD=15THENPRINT@256,"THE SOUND OF A TREMENDOUS AMOUNT OF WATER RUNNING!!";:GOSUB413:PRINT@256,"A SOLID STEEL GATE SLAMMED DOWN-BLOCKING THIS ROOM'S ONLY EXIT!";:L2(3)=0:MP=1:D(16)=8:UD(3)=0:GOTO38
169 IFB=1ANDD=15THENPRINT@256,"IT GOES UP THROUGH A VERY SMALL HOLE IN THE CEILING";:GOTO38
170 IFB=21ANDD=15ANDMN=0THENB=16:GOTO168
171 IFB=23ANDC=4ANDC(4)=1THENX=USR(0):PRINT@256,"TO WHAT";:INPUTA$:X=USR(0):IFA$=D$(15)THENPRINT@256,"OK";:MO=1:GOTO38
172 IFB=16ANDC=4ANDMO=1THENB=16:D=15:GOTO168
173 IFB=1ANDD=16ANDMN=1THENPRINT@256,"MIRROR SURFACE,NO SCRATCHES";:GOTO38
174 IFB=1ANDD=15THENPRINT@256,"BRONZE,ABOUT 1/4'' DIAMETER":GOTO600
175 IF(B=10OR B=19OR B=5OR B=2)ANDD=16THENPRINT@256,"YOU'RE KIDDING";:GOTO38
176 IFH1=2THEN177ELSE181
177 IFB=1ANDD=30THENPRINT@256,"IMMENSE BRONZE CRYPT.SCENES FROMTHE LIFE OF FLODNAR ARE RELIEVEDIN THE METAL.THERE IS SCRIPT ON THE NEAR END";:D(31)=15:GOTO38
178 IFB<>1AND D=30THENPRINT@332,"KABOOM!!";:GOSUB412:X=USR(0):PRINT@256,"A LIGHTNING BOLT CAME OUT OF THECRYPT AND STRUCK ME IN THE HEAD!";:GOTO442
179 IF(B=19OR B=22 ORB=16OR B=8ORB=9)ANDD=(37-JI)THENIF(C(49)=0AND C(50)=0)THENPRINT@256,"A SECTION SPUN AROUND!!";:C(50)=15:C(49)=15:GOTO38ELSEPRINT@256,"A SECTION SPUN AROUND";:GOTO38
180 IFB=1ANDD=31THENPRINT@256,"'DISTURB NOT THIS WARRIOR'S RESTOR SUFFER MY WRATH'---HERMAN";:GOTO38
181 REM
182 GOTO235
183 REM V4
184 IFH1=5THEN185ELSE186
185 IFB=21ANDMK=1THENPRINT@426,"CLIMBING";:GOSUB411:PRINT@394,"CLIMBING";:GOSUB411:PRINT@362,"CLIMBING";:GOSUB411:PRINT@330,"CLIMBING";:GOSUB411:PRINT@295,"ALMOST THERE";:GOSUB411:PRINT@256,"MADE IT!!";:L=31:V=3:H=28:C(4)=36:C(8)=36:GOTO38
186 IFH1=2THEN187ELSE198
187 IFJC=0THENFL$="ONE JEW-ELED EYE STARES AT ME,THE OTHER IS AN EMPTY SOCKET.":D$(26)="EYE":ELSEFL$="TWO JEW-ELED EYES STARE.":D$(26)="EYES"
188 IA=0:IFB=4ANDC=13ANDC(13)=1ANDP$=D$(24)THENJB=1:PRINT@256,"OK";:HA=HA-1:C(13)=16:IA=1
189 IFB=4ANDC=21ANDC(21)=1ANDP$=D$(24)THENJA=1:PRINT@256,"OK";:HA=HA-1:C(21)=16:IA=1
190 IFB=4ANDC=37ANDC(37)=1ANDP$=D$(25)THENJC=1:PRINT@256,"OK";:HA=HA-1:C(37)=16:D$(26)=D$(26)+"S":D(25)=0:IA=1
191 IFJD=0THENIFJA=1ANDJB=1ANDJC=1THENJD=1:UD(11)=1:GOSUB412:PRINT@325,"THE ";D$(22);" OPENED!!";:GOTO291ELSEIFIA=1THEN38
192 IFB=1ANDD=22THENPRINT@256,"HUGE-ALMOST TO THE CEILING,THEREIS A BOWL IN IT'S HANDS,";FL$;"IT IS AGAINSTTHE NORTH WALL";:D(24)=16:D(25)=16:D(26)=16:GOTO38
193 IFB=1ANDD=24THENPRINT@256,"ALMOST 3' IN DIAMETER";:GOTO38
194 IFB=1ANDD=26THENPRINT@256,D$(23);:GOTO38
195 IFB=1ANDD=25ANDJC=0THENPRINT@256,"JUST AN EMPTY ";D$(25);:GOTO38
196 IFB=1ANDD=25THENPRINT@256,"IT IS NO LONGER EMPTY";:GOTO38
197 IFB=22ANDD=22THENPRINT@256,"I'LL TRY,IF YOU HAVE A TRUSS AVAILABLE";:GOTO38
198 IFH1=1THEN199ELSE202
199 IFB=1ANDD=27THENIFJG=0THENPRINT@256,"LARGE IRON CHEST,LID IS DOWN.ON THE SURFACE ARE ENGRAVINGS OF A KNIGHT CONQUERING VARIOUS FOES";:GOTO38ELSEPRINT@256,"THE LID IS OPEN";:GOTO38
200 IFB=5ANDD=27THENIFJH=0THENPRINT@256,"OH NO!!":GOSUB412:PRINT@288,"ALL THE EVIL HE HAD CONQUERED ISNOW ATTACKING me!!!!";:GOSUB413:GOTO442ELSEPRINT@256,"IT IS OPEN";:C(42)=9:JG=1:GOTO38
201 IFB=13ANDD=28ANDC(27)=1THENJH=1:GOTO38
202 REM
203 GOTO235
204 REM V5
205 IFH1=6ANDB=1ANDD=10THENPRINT@256,"SPOKED,METAL WHEEL ABOUT 18 IN. IN DIAMETER,LOCATED ON THE EAST WALL";:GOTO38
206 IFH1=6ANDB=8ANDD=10THENIFMF=0THENMF=1:W1$=L$(16):L$(16)="HALLWAY":L2(16)=10:D(12)=0:PRINT@256,"SCREECH";:GOTO38ELSEIFMF=1THENMF=0:L$(16)=W1$:L2(16)=3:PRINT@256,"SQUEAL";:D(12)=21:GOTO38
207 IFH1=3THEN208ELSE211
208 IFB=1ANDD=17THENPRINT@256,"ORNATE CASTING,SOLID-EXCEPT FOR SMALL ORIFICE IN THE CENTER";:D(18)=24:GOTO38
209 IFB=1ANDD=18THENPRINT@256,"A LOCK SEEMS TO BE INSIDE";:GOTO38
210 IFB=5ANDD=17THENIFC(5)=1THENPRINT@256,"'RATTLE'";:GOSUB411:PRINT@288,"'SCRAPE'";:GOSUB411:PRINT@320,"'CLICK'";:GOSUB411:PRINT@352,"'RATTLE'";:GOSUB411:PRINT@384,"'CLANK'";:GOSUB411:PRINT@256,"IT SWUNG OPEN!";:MS=1:L2(19)=2:S=1:GOTO38
211 IFH1=1THEN212ELSE214
212 IFB=1ANDD=29THENPRINT@256,"THERE'S AN ENGRAVING OF A KNIGHTON A GREAT STEED,FIGHTING A HUGEDRAGON-HARD TO TELL WHO'S AHEAD";:GOTO38
213 IFB=5ANDD=29THENPRINT@256,"'SCREEEEEEECH'";:GOSUB412:PRINT@320,"IT'S OPEN";:L2(5)=11:GOTO291
214 IFH1=4ANDB=1ANDD=50THENPRINT@256,"IT'S HARD TO MAKE OUT,IT SEEMS  TO BE WRITTEN IN BLOOD----------'DON'T PET THE DRAGON'";:GOTO38
215 GOTO235
216 REM V6
217 IFH1=2THEN218ELSE221
218 IFB=1ANDD=6THENPRINT@256,"ONLY YOU CAN KEEP OUR ADVENTURE CLEAN-PUT UNWANTED ITEMS IN THE TRASH CHUTE";:D(7)=18:GOTO38
219 IFB=1ANDD=7ANDD(7)=18THENPRINT@256,"HUGE OPENING IN WALL-CAN'T SEE THE BOTTOM";:GOTO38
220 IFB=4ANDP$="TRASH CHUTE"ANDC(C)=1THENC(C)=73:GOSUB463:HA=HA-1:PRINT@256,"AFTER A LONG WAIT,I HEARD IT HITBOTTOM,THEN THERE WAS A BRIGHT  FLASH OF LIGHT FROM THE DEPTHS  OF THE CHUTE";:IFC=1THENQA=0:GOTO38ELSEIFC=2THENQB=0:GOTO38ELSEGOTO38
221 IFH1=3THEN222ELSE224
222 IFB=8ANDD=19THENIFMT=0THENMT=1:L2(18)=6:L2(17)=8:D(20)=23:D(21)=22:PRINT@256,"'SCREEEEECH'";:GOTO38ELSEMT=0:L2(18)=3:L2(17)=3:D(20)=0:D(21)=0:PRINT@256,"'RUMBLE'";:GOTO38
223 IFB=1ANDD=19THENPRINT@256,"MOUNTED ON THE SOUTH WALL,IRON- ABOUT 3' LONG";:GOTO38
224 REM
225 GOTO235
226 REM V7
227 IFH1=1ANDD=34THENPRINT@256,"FROM HERE YOU MAY GO BACK TO KRAGNOR'S BY INPUTING <R> OR ENTER THE GAME BY INPUTING <U>OR<E> AS YOU DESIRE";:GOTO38
228 IFH1=6THEN229ELSE231
229 IFB=1ANDD=35THENPRINT@256,"A THREE FOOT CUBE";:GOTO38
230 IFB=5ANDD=35THENPRINT@256,"IT is OPEN AND IT IS ALSO EMPTY";:GOTO38
231 IFH1=2THEN232ELSE234
232 IFB=1ANDD=36THENPRINT@256,"THERE IS A DRAWING OF A FLAME ONTHE DOOR";:GOTO38
233 IFB=5ANDD=36ANDJK=0THENPRINT@256,"IT IS NOW OPEN";:JK=1:L2(14)=13:GOTO291
234 IFH1=6ANDB=4ANDP$=D$(35)ANDC(C)=1THENPRINT@256,"I PUT IT IN";:C(C)=46:GOSUB412:PRINT@320,"A WEASEL JUMPED OUT OF THE BOX  WITH IT IN HIS MOUTH AND RAN OFFTO THE EAST!!";:HA=HA-1:GOTO38
235 ER$(1)="I DON'T SEE ANYTHING SPECIAL":ER$(2)="LOOKS NORMAL TO ME":ER$(3)="SEEMS ORDINARY":ER$(4)="LOOKS USUAL":ER$(5)="APPEARS REGULAR":ER$(6)="COMMONPLACE":ER$(7)="UNDISTINGUISHED":IFB=1AND(D=1OR D=2OR D=3OR D=5) THENPRINT@256,"SOLID STONE";:GOTO38
236 IFD>50THEND=0ELSEIFC>50THENC=0ELSEIFB=1AND(D(D)=LL OR C(C)=LL OR C(C)=1)THENR2=RND(7):PRINT@256,ER$(R2);:GOTO38ELSEIFB=1THENPRINT@256,"I DON'T SEE IT";:GOTO62
237 IFB=4THENPRINT@256,"I DON'T KNOW HOW";:GOTO38
238 IFB=9THENPRINT@256,"NO EFFECT";:GOTO38
239 IFB=7THEN446ELSEIFB=19ANDD=1THENPRINT@256,"THIS MUST NOT BE THE PLACE";:GOTO38
240 IFB=12THENPRINT@256,"OK";:GOTO38
241 IFB=14THENPRINT@256,"WON'T BURN";:GOTO38
242 IFB=15THENPRINT@256,"DON'T HEAR ANVTHING BUT MY OWN  HEART!";:GOTO38
243 IFB=21THENPRINT@256,"I'M NOT A MONKEY";:GOTO38
244 IFB=11THENPRINT@256,"OUCH!!";:GOTO38
245 PRINT@256,"I CAN'T DO THAT AT THE MOMENT";:GOTO69
246 REM DAMAGE
247 S1=INT(S3):IFS1>22THENS1=22
248 IFS1=S2 THENRETURNELSES2=S1
249 IFS1<5THENCC=191:CE=0:IFCD=0THENSOUND200,3:CD=1
250 IFS1<9 AND S1>4 THENCC=159:CD=0:IFCE=0THENSOUND100,3:CE=1
251 IFS1>8THENCC=175:CE=0
252 IFS3<-1THENGOTO442
253 FORX=1193TO1193+S1:POKEX,CC:NEXT
254 IFS1=22THENRETURN
255 FORX=1194+S1 TO1215:POKEX,208:NEXT:RETURN
256 REM MACHINE LANGUAGE ROUTINE
257 DATA 134,223,142,4,9,167,128,140,4,32,38,249,142,4,38,167,128,140,4,64,38,249,142,4,135,167,128,140,4,160,38,249,142,4,70,167,128,140,4,128,38,249,134,207,142,4,224,167,128,140,5,224,38,249,57
258 DATA LOOK,SEARCH,GET,TAKE,PICK UP,PUT DOWN,PUT,PLACE,OPEN,UNLOCK,CLOSE,LOCK,EAT,DRINK,TURN,TWIST,ROTATE,HIT,STRIKE,BREAK,LIFT,DROP,KICK,JUMP,SAY,BURN,LIGHT,LISTEN,PULL,THROW,STAB,PUSH,GIVE,CLIMB,MOVE,INSERT,TIE
259 DATA SACK,ROPE,THIEVE'S TOOLS,ARMOR,SHIELD,POLE,UNICORN HORN,DRAGON'S TEARS
260 DATA TINDER&FLINT,TORCH,ORNG LQD,BLUE LQD,BOOK,HAMMER,LANTERN,WATER BAG,WINE BAG,MAP,ORNG PWDR,RATIONS,MIRROR,DRAGON GOODIES,FLUTE,SPEAR,MAGIC SPELL,KEY,MARBLE,PINE CONE
261 DATA GOLD BRACELET,STAR SAPPHIRE,EMERALD,RUBY,DIAMOND,TURQUOISE,ORANGE SAPPHIRE,TOPAZ,OPAL,JADE BRACELET
262 DATA BLACK PEARL,BLUE SAPPHIRE,JADE PENDANT,GOLD CHAIN,SILVER BAR,PLATINUM RING,IVORY BROOCH,BRONZE IDOL,GOLD CROWN,CRYSTAL WAND
263 DATA WALL,CEILING,ROOF,WOODEN DOOR,FLOOR,SIGN,TRASH CHUTE,TRAP DOOR,DRAGON,WHEEL
264 DATA LEVERS,FLAMES,HOLE IN FLOOR,HOLE IN CEILING,CHAIN,STEEL GATE,BRONZE DOOR,ORIFICE,CRANK,HOLE IN WALL
265 DATA OPENING IN WALL,STATUE,SAPPHIRE (ORANGE),BOWL,SOCKET,EYE,CHEST,HAWAYA,STEEL DOOR,CRYPT
266 DATA SCRIPT,OPENING,NOTICE,NOTE,TIN BOX,TIN DOOR,CABINET,DRAWER,ADMONITION,NICHE
267 DATA LATCH,CUBBY HOLE,PADLOCK,HASP,HOOK,DIAL,BRASS DOOR,STEEL GRATING,WRITING,SCRAWL
268 DATA 1,1,2,2,2,3,4,4,5,5,6,6,7,7,8,8,8,9,9,9,10,3,11,12,13,14,14,15,16,17,18,19,20,21,22,4,23
269 DATA NONE,N,S,E,W,N S,N E,N W,S E,S W,E W,N S E,N S W,N E W,S E W,N S E W
270 DATA SMALL ROOM,HALL,SMALL ROOM,STOREROOM,HALL,FLOODED ROOM,ENTRANCE
271 DATA HALL,HALL,CRYPT OF FLODNAR,SMALL ROOM,HALL,SMALL ROOM,HALL
272 DATA EXIT,HALL BLOCKED BY FLAME,SMALL ROOM,HALL,LARGE ROOM,HEXAGON ROOM,HALL
273 DATA VAULT,HALL,HALL,LARGE CHAMBER,HALL,HALL,HALL
274 DATA HALL,HALL,ROOM W/HOLE IN FLOOR,ROOM W/HOLE IN CEILING,HALL,HALL,HALL
275 DATA TRIANGULAR ROOM,EMPTY ROOM,HALL BLOCKED BY DRAGON,HALL,PENTAGON ROOM,STOREROOM,HALL
276 DATA CIRCULAR ROOM,HALL,HALL,HALL,HALL,HALL,HALL
277 DATA 3,3,16,2,8,16,3
278 DATA 10,10,16,2,7,2,10
279 DATA 4,3,3,3,16,1,10
280 DATA 2,13,10,10,8,5,7
281 DATA 3,16,4,4,4,2,6
282 DATA 10,8,1,1,16,3,10
283 DATA 4,9,1,2,1,9,7
284 DATA 0,2,1,0,0,2,1
285 DATA 2,1,2,0,0,0,0
286 DATA 0,0,0,2,1,0,0
287 DATA 0,2,1,0,0,0,0
288 DATA 2,1,0,0,2,1,0
289 DATA 0,0,0,2,1,0,0
290 DATA 0,0,2,1,2,1,0
291 L=H+V:PRINT@9,L$(L);
292 GOSUB414
293 IFUP=1THENPRINT@58,"U";
294 IFDO=1THENPRINT@61,"D";
295 N=0:S=0:E=0:W=0:IFL2(L)=16THEN38
296 IFL2(L)=0THEN38
297 PRINT@43,O$(L2(L));
298 IFL2(L)=0THEN38
299 IFL=25ANDMR=0THENPRINT@256,"'CRUNCH'";:PRINT@320,"THERE IS SAND ON THE FLOOR";
300 ON L2(L) GOTO301,302,303,304,305,306,307,308,309,310,311,312,313,314,315
301 N=1:GOTO38
302 S=1:GOTO38
303 E=1:GOTO38
304 W=1:GOTO38
305 N=1:S=1:GOTO38
306 N=1:E=1:GOTO38
307 N=1:W=1:GOTO38
308 S=1:E=1:GOTO38
309 S=1:W=1:GOTO38
310 E=1:W=1:GOTO38
311 N=1:S=1:E=1:GOTO38
312 N=1:S=1:W=1:GOTO38
313 N=1:E=1:W=1:GOTO38
314 S=1:E=1:W=1:GOTO38
315 N=1:S=1:E=1:W=1:GOTO38
316 REM LOOK SACK
317 L1=256:L2=0:L3=287:L4=0
318 IFC(3)<>1THENPRINT@256,"I DON'T HAVE THE SACK";:GOTO38
319 FORX=1TO50:IFC(X)=2THEN320ELSENEXT:IFL4=0THENPRINT@256,"AS EMPTY AS KRAGNOR'S HEART";:GOTO38ELSEGOTO38
320 IFX>50THENRETURNELSEL2=LEN(C$(X))
321 IFL1+L2>L3 THENL3=L3+32:L1=L3-31
322 PRINT@L1,C$(X);:L4=L4+1:L1=L1+1+L2
323 NEXT:GOTO38
324 REM WEAPON CHECK
325 IFA$="Q"ANDC(1)<>1THENPRINT@384,"UNFORTUNATELY,I DO NOT HAVE MY";:PRINT@416,"TRUSTWORTHY ";C$(1);:GOTO38
326 IFA$="P"ANDC(2)<>1THENPRINT@384,"I DON'T SEEM TO HAVE MY";:PRINT@416,C$(2);" WITH ME";:GOTO38
327 GOTO420
328 REM
329 REM I SEE
330 L1=70:L2=0:L3=95:L4=0:LL=L+5:FORX=1TO50:IFC(X)=LL THEN331ELSENEXT:GOTO333
331 L2=LEN(C$(X)):IFL2+L1>L3 THENL3=L3+32:L1=96:IFL3=159THENPRINT@384,"***OVERFLOW OF ITEMS IN SIGHT***";:RETURN
332 PRINT@L1,C$(X);:L1=L2+1+L1:NEXT
333 FORX=1TO50:IFD(X)=LL THEN334ELSENEXT:RETURN
334 L2=LEN(D$(X)):IFL2+L1>L3 THENL3=L3+32:L1=96:IFL3=159THENPRINT@384,"***OVERFLOW OF ITEMS IN SIGHT***";:RETURN
335 PRINT@L1,D$(X);:L1=L2+1+L1:NEXT:RETURN
336 REM I HAVE
337 L1=135:L2=0:L3=0:L4=0:FORX=1TO50:IFC(X)=1THEN338ELSENEXT:RETURN
338 L2=LEN(C$(X)):L4=L1+L2+1:IFL4>159THEN340ELSEPRINT@L1,C$(X);:L3=L3+1:IFL3=4THENRETURN
339 L1=L1+L2+1:NEXT:RETURN
340 PRINT@416,"***OVERFLOW OF ITEMS IN HAND****";:RETURN
341 REM DROP ITEM
342 IFC=6AND C(6)=1THENHA=HA+1
343 IFC(C)=1THENC(C)=L+5 ELSEPRINT@256,"I DON'T HAVE IT";:GOTO38
344 IFC<>3THENHA=HA-1:GOTO38
345 FORX=1TO20:IFC(X)=2THENC(X)=3
346 NEXT:GOTO38
347 REM GET
348 IFC(C)=2ANDC(3)<>1THENPRINT@256,"I DON'T HAVE THE SACK";:GOTO38
349 IFC=37ANDL=11ANDJC=1THENJC=0:D(25)=16
350 IFC=13ANDL=11ANDJB=1THENJB=0
351 IFC=21ANDL=11ANDJA=1THENJA=0
352 IFJD=1AND(JA=0OR JB=0OR JC=0)THENPRINT@365,"SLAM!!";:JD=0:UD(11)=0
353 IFC=4ANDL=32ANDMK=1THENPRINT@256,"IT'S ATTACHED TO THE POLE ABOVE-I CAN'T";:GOTO38
354 IF(C=4OR C=8)ANDL=31ANDMK=1THENMK=0:PRINT@384,"I UNTIED THE ROPE";
355 IFL=3ANDMO=1THENMO=0:PRINT@256,"I UNTIED THE ROPE";
356 IFC=4ANDMN=1THENPRINT@256,"I CAN'T-IT'S STUCK UNDER THE    STEEL GATE";:GOTO38
357 IFC=0THEN364
358 IFC(C)=1THENPRINT@256,"I ALREADY HAVE IT";:GOTO38
359 IFHA>1ANDC<>2THENPRINT@256,"MY HANDS ARE FULL";:GOTO38
360 IFC(C)=L+5THENC(C)=1:HA=HA+1:IFC=3OR C(C)=2THEN361ELSEIFC=6THENHA=HA-1:GOTO38ELSE38
361 REM
362 IFC=3 THENHA=HA-1:FORX=1TO50:IFC(X)=3THENC(X)=2:NEXT:GOTO38:ELSENEXT:GOTO38
363 IFC(C)=2THENC(C)=1:HA=HA+1:KC=KC-1:GOTO38
364 PRINT@256,"I CAN'T GET IT";:GOTO38
365 REM PUT IN SACK
366 IFJF=1ANDC(3)=1 THENPRINT@256,"THE SACK IS RIPPED-IT FALLS OUT";:GOSUB461:GOTO38
367 IFC(3)<>1THENPRINT@256,"I DON'T HAVE THE SACK";:GOTO38
368 IFKC=>CK THENPRINT@256,"THE SACK IS FULL";:GOTO38
369 IFC=8ANDC(8)=1THENPRINT@256,"IT WON'T FIT-IT'S TOO LONG";:GOTO38
370 IFC=6ANDC(6)=1THENPRINT@256,"IT WON'T FIT";:GOTO38
371 IFC(C)=1THENIFC=3THENPRINT@256,"THAT DOESN'T MAKE SENSE";:GOTO38:ELSEC(C)=2:KC=KC+1:HA=HA-1:GOTO38
372 PRINT@256,"I CAN'T PUT IT IN THE SACK";:GOTO38
373 REM SAVE GAME
374 JV=0:FORQ=31TO50
375 IFC(Q)=1THENJV=JV+1
376 IFC(Q)=2THENJV=JV+1
377 IFC(Q)=3THENJV=JV+1
378 NEXT
379 IFJV=20THEN458
380 CLS2:PRINT@256,"POSITION TAPE,PRESS RECORD+PLAY.WHEN READY,PRESS ENTER";
381 A$=INKEY$:IFA$=""THEN381
382 CLS6:PRINT@267,"SAVING DATA";
383 OPEN"O",#-1,"DATA"
384 PRINT#-1,MA,MB,MC,MD,ME,MF,MG,MH,MI,MJ,MK,ML,MM,MN,MO,MP,MQ,MR,MS,MT,UP,DO,L,LL,V,H,SK,CK,JA,JB,JC,JD,JE,JF,JG,JH,JI,JJ,JK,JL,JM,JN,JO,JP,JQ,JR,JS,W1$,S3,S5,KN,HA,QA,QB,GP,E,W,N,S,CO,KC,N1,JT,JU,JV,JW
385 PRINT#-1,C$(1),C$(2)
386 FORX=1TO49:PRINT#-1,L2(X),UD(X):NEXT
387 FORX=1TO100:PRINT#-1,W(X):NEXT
388 PRINT#-1,L$(6),L$(16),L$(38)
389 FORX=1TO50:PRINT#-1,D(X),C(X):NEXT
390 CLOSE#-1
391 PRINT@448,"THE DATA HAS BEEN SAVED";
392 GOSUB412:CLS2:PRINT@194,"USE THIS TAPE WHEN ENTERING         KRAGNOR'S STORE";:PRINT@384,"THE DATA TAPE HAS BEEN MADE. NOWTURN COMPUTER OFF,WAIT 1 MINUTE,TURN COMPUTER ON,CLOAD 'KRAGNOR'";
393 GOTO393
394 FORX=1TO2000:NEXT:X=USR(0):GOTO38
395 REM ENTER OLD GAME
396 CLS7:PRINT@97,"POSITION DATA TAPE FROM KRAGNOR";:PRINT@161,"PRESS PLAY. PRESS <ENTER> WHEN";:PRINT@236,"READY";
397 A$=INKEY$:IFA$=""THEN397
398 CLS5:PRINT@266,"READING DATA";
399 OPEN"I",#-1,"DATA"
400 INPUT#-1,MA,MB,MC,MD,ME,MF,MG,MH,MI,MJ,MK,ML,MM,MN,MO,MP,MQ,MR,MS,MT,UP,DO,L,LL,V,H,SK,CK,JA,JB,JC,JD,JE,JF,JG,JH,JI,JJ,JK,JL,JM,JN,JO,JP,JQ,JR,JS,W1$,S3,S5,KN,HA,QA,QB,GP,E,W,N,S,CO,KC,N1,JT,JU,JV,JW
401 INPUT#-1,C$(1),C$(2)
402 FORX=1TO49:INPUT#-1,L2(X),UD(X):NEXT
403 FORX=1TO100:INPUT#-1,W(X):NEXT
404 INPUT#-1,L$(6),L$(16),L$(38)
405 FORX=1TO50:INPUT#-1,D(X),C(X):NEXT
406 CLOSE#-1
407 CLS:PRINT@260,"THE DATA HAS BEEN INPUT";:PRINT@428,"ONE MOMENT,PLEASE";
408 FORX=1TO3000:NEXT
409 GOTO15
410 FORX=1TO2000:NEXT:X=USR(0):GOTO38
411 FORX=1TO900:NEXT:X=USR(0):RETURN
412 FORX=1TO2000:NEXT:X=USR(0):RETURN
413 FORX=1TO4000:NEXT:X=USR(0):RETURN
414 REM UP+DOWN
415 IFUD(L)=1THENUP=1:DO=0:GOSUB418:RETURN
416 IFUD(L)=2THENUP=0:DO=1:GOSUB419:RETURN
417 DO=0:UP=0:RETURN
418 PRINT@449,"A CURVING STAIRWAY GOES UPWARD";:RETURN
419 PRINT@448,"A CURVING STAIRWAY GOES DOWNWARD";:RETURN
420 REM FIGHT
421 S9=S3
422 IFL=38ANDME=0THEN424
423 PRINT@256,"THERE IS NOTHING HERE TO FIGHT";:GOTO38
424 L2(38)=0:N2=RND(6)+RND(6)+RND(6)+RND(6)+RND(6):IFN2=5OR N2=30THENPRINT@256,"A LUCKY BLOW! THE DRAGON IS DEAD";:ME=1:L$(38)="HALLWAY":L2(38)=5:S=1:GOTO38ELSEN=0
425 N2=RND(5):IFN2=1THENPRINT@256,"IT MISSED!";ELSEIFN2=2THENPRINT@256,"IT HIT ME WITH IT'S TAIL";:S3=S3-2:ELSEIFN2=3THENPRINT@256,"IT RAKED MY ARM WITH IT'S CLAW";:S3=S3-3:ELSEIFN2=4THENPRINT@256,"MY LEG WAS SMASHED BY IT'S WING";:S3=S3-4
426 IFN2=5THENPRINT@256,"A BLAST OF FLAME CAUGHT ME!";:S3=S3-5
427 IFS3<0THEN442
428 N2=RND(5):IFN2=1THENPRINT@288,"I MISSED COMPLETELY!";:ELSEIFN2=2THENPRINT@288,"I HIT IT'S FOREARM";:N1=N1-2:ELSEIFN2=3THENPRINT@288,"I CONNECTED WITH IT'S NECK";:N1=N1-3:ELSEIFN2=4THENPRINT@288,"A SOLID BLOW TO THE BODY";:N1=N1-4
429 IFN2=5THENPRINT@288,"SMACKED IT'S HEAD SOUNDLY!!";:N1=N1-5
430 IFS3<S9 THENIFA$="Q"THENS3=S3+INT(JN):IFS3>S9THENS3=S9
431 IFS3<S9 THENIFA$="P"THENS3=S3+INT(JO):IFS3>S9 THEN S3=S9
432 IFN1<1THENPRINT@355,"!!!THE DRAGON IS DEAD!!!";:S=1:ME=1:L2(38)=5:L$(38)="HALLWAY":N=1:GOTO38
433 GOTO38
434 REM HELP
435 PRINT@256,"PERHAPS A TRIP BACK TO KRAGNOR'STO PURCHASE A NEEDED ITEM?";:GOTO38
436 IFA9$="A"THENCO=CO+1
437 IFA7$="C"THENCO=CO+1
438 IFA8$="C"THENCO=CO+1
439 RETURN
440 REM
441 GOTO38
442 REM DEATH
443 FORX=1TO2000:NEXT:CLS0:FORX=1TO500:NEXT:PLAY"O1;T1;L1BP10BAP4GP10GL1.C"
444 CLEAR:PRINT@230,"PERHAPS,ANOTHER TRY?";:FORX=1TO20000:NEXT:END
445 REM ENTER TAPE
446 REM EAT
447 IFC(22)<>1AND C(18)<>1AND C(19) <>1THEN457
448 IFC(22)=1THENJM=JM-1:HA=HA-1:IFJM<1THENC(22)=0
449 IFC(18)=1THENJR=JR-1:HA=HA-1:IFJR<1THENC(18)=0
450 IFC(19)=1THENJQ=JQ-1:HA=HA-1:IFJQ<1THENC(19)=0
451 IFS3>18THENPRINT@256,"I REALLY DIDN'T NEED IT";
452 IFS3>12AND S3<19THENPRINT@256,"REFRESHING";
453 IFS3>8AND S3<13THENPRINT@256,"I NEEDED THAT!";
454 IFS3>3AND S3<9THENPRINT@256,"BURP":GOSUB412:PRINT@256,"very much needed";
455 IFS3<4THENPRINT@256,"I THOUGHT I WAS A GONER";
456 S3=25:GOTO38
457 PRINT@256,"I DON'T HAVE ANYTHING IN MY HANDTO EAT OR DRINK";:GOTO38
458 CLS6:PRINT@104,"CONGRATULATIONS!";:PRINT@169,"YOU HAVE FOUND";:PRINT@230,"ALL TWENTY TREASURES";:PRINT@295,"IN THIS ADVENTURE!";:PRINT@391,"CARE TO TRY AGAIN?";
459 FORX=1TO5:PLAY"T7;O4;L1AL2.AL4GL2GFL1CL4.CL8DL4.EL8CL2DCL4.CL8DL4.EL8CL2DCL1AL2.AL4GL2GFL1CL4.CL8DL4.EL8FL2GO5CO4L1.FP1":NEXT:CLEAR:END
460 L2(6)=1:D(16)=8:L$(6)="LARGE ROOM W/WET FLOOR":C(30)=11:RETURN
461 IFC=3THENRETURN
462 HA=HA-1:C(C)=LL:RETURN
463 IFC<>3THENRETURN
464 IFC(1)=2THENQA=0
465 IFC(2)=2THENQB=0
466 HA=HA+1
467 FORZU=1TO50:IFC(ZU)=2THENC(ZU)=79
468 NEXT:RETURN
